# -*- python -*-
Import("env")
import os
import sys

import lsst.tests
import lsst.daf.persistence
import lsst.SConsUtils as scons # scons 1.17 supports "env.ProductDir", but for now use scons.ProductDir

env.Program(["Mask_1.cc"], LIBS=env.getlibs("afw"))
env.Program(["MaskedImage_1.cc"], LIBS=env.getlibs("afw"))
env.Program(["MaskedImageIO_1.cc"], LIBS=env.getlibs("afw"))
env.Program(["MaskedImageIO_2.cc"], LIBS=env.getlibs("afw"))
env.Program(["MaskIO_1.cc"], LIBS=env.getlibs("afw"))
# Removed since not ready according to Tim Axelrod. 2007-11-26
# env.Program(["MaskIO_2.cc"], LIBS=env.getlibs("afw"))
env.Program(["SourceIO_1.cc"], LIBS=env.getlibs("afw"))
env.Program(["SourceIO_2.cc"], LIBS=env.getlibs("afw"))
env.Program(["wcsCopy.cc"], LIBS=env.getlibs("afw"))

env.CheckSwig("python", ilang="c++",includedProducts="utils")
env.Append(SWIGFLAGS=" -Ipython")

#env["SWIGFLAGS"] += " -I%s/%s" % (os.environ["MWI_DIR"], "python")
afwTests = env.LoadableModuleIncomplete("_afwTests", "afwTests.i", LIBS=env.getlibs("afw"))

#
# Tests
#
dataDir = scons.ProductDir("afwdata")
if not dataDir:
     print "Warning: afwdata is not set up; not running the tests!"

else:
    # copy AFWDATA_DIR to the test's environment
    env["ENV"]["AFWDATA_DIR"] = os.environ["AFWDATA_DIR"]
    
    if lsst.daf.persistence.DbAuth.available():
        env["ENV"]["SEAL_PLUGINS"] = os.environ["SEAL_PLUGINS"]    
        if os.environ.has_key("LSST_DB_AUTH"):
            env["ENV"]["LSST_DB_AUTH"] = os.environ["LSST_DB_AUTH"]
    else:
        print "WARNING: No fallback database authenticator seen"
        print "Database tests may be skipped or may fail"

    # set up arguments for C++ tests
    inFile = os.path.join(dataDir, "871034p_1_MI")
    testArgs = dict(
        MaskedImage_1 = "%s %s file:MaskedImage_1_output_1 file:MaskedImage_1_output_2" % (inFile, inFile),
        MaskedImageIO_1 = inFile,
        MaskedImageIO_2 = inFile,
        MaskIO_1 = "%s_msk.fits" % inFile,
    )
    tests = lsst.tests.Control(env, args = testArgs, ignoreList=['afwTests.py'], verbose = True)
    tests.run("*.cc")
    
    # set up python tests
    for target in tests.run("*.py"):
        env.Depends(target, afwTests)
        env.Depends(target, "../python/lsst/afw/detection/_detectionLib%s" % (env["LDMODULESUFFIX"]))
        env.Depends(target, "../python/lsst/afw/display/_displayLib%s" % (env["LDMODULESUFFIX"]))
        env.Depends(target, "../python/lsst/afw/image/_imageLib%s" % (env["LDMODULESUFFIX"]))
        env.Depends(target, "../python/lsst/afw/math/_mathLib%s" % (env["LDMODULESUFFIX"]))

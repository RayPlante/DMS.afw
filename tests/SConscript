# -*- python -*-
Import("env")
import os, re, sys
import glob

import lsst.tests
import lsst.daf.persistence
import lsst.SConsUtils as scons # scons 1.17 supports "env.ProductDir", but for now use scons.ProductDir

for fileName in glob.glob("*.cc"):
    if fileName == "MaskIO_2.cc":       # This test appears to be invalid
        continue

    if re.search(r"_wrap.cc", fileName): # not designed to be independently built!
        continue

    env.Program(fileName, LIBS=env.getlibs("afw"))

env.CheckSwig("python", ilang="c++",
              includedProducts="afw utils daf_base pex_exceptions pex_logging pex_policy daf_persistence daf_data security")

testLib = env.LoadableModuleIncomplete("_testLib", ["testLib.i"], LIBS=env.getlibs("afw"))

#
# Tests
#
dataDir = scons.ProductDir("afwdata")
if not dataDir:
    print "Warning: afwdata is not set up; not running the tests!"
else:
    # copy AFWDATA_DIR to the test's environment
    env["ENV"]["AFWDATA_DIR"] = os.environ["AFWDATA_DIR"]
    
    if lsst.daf.persistence.DbAuth.available():
        if os.environ.has_key("LSST_DB_AUTH"):
            env["ENV"]["LSST_DB_AUTH"] = os.environ["LSST_DB_AUTH"]
    else:
        print "WARNING: No fallback database authenticator seen"
        print "Database tests may be skipped or may fail"

    # set up arguments for C++ tests
    inFile = os.path.join(dataDir, "871034p_1_MI")
    testArgs = dict(
        MaskedImage_1 =   "%s %s file:MaskedImage_1_output_1 file:MaskedImage_1_output_2" % (inFile, inFile),
        MaskedImageIO_1 = inFile,
        MaskedImageIO_2 = inFile,
        MaskIO_1 =         "%s_msk.fits" % inFile,
    )

    ignoreList=["testLib.py"]
    tests = lsst.tests.Control(env, args = testArgs, ignoreList=ignoreList, verbose = True)

    tests.run("*.cc")

    # set up python tests
    for target in tests.run("*.py"):
        for pkgName in ("detection", "display", "image", "math"):
            env.Depends(target, os.path.join(
                "#python", "lsst", "afw", pkgName, "_%sLib%s" % (pkgName, env["LDMODULESUFFIX"])))
        if target.endswith("SpatialCell.py"):
            env.Depends(target, "_testLib%s" % (env["LDMODULESUFFIX"]))
            env.Depends(target, "testLib.py")
